// ############### Mapscripts ###############

mapscripts NationalHistoryMuseumL1_MapScripts {
    MAP_SCRIPT_ON_RESUME: NHML1_OnResume
	//MAP_SCRIPT_ON_FRAME_TABLE [
		//VAR_TEMP_2, 0: NHML1_EveryFrame
	//]
}

script NHML1_OnResume {
	setvar(VAR_TEMP_2, 0) //var for EveryFrame
	call(load_HONOR_MODE)
	if (!flag(FLAG_NHML1_BOULDER_FELL_DOWN)) {
        setflag(FLAG_NHML1_BOULDER)
	}
	else {
		clearflag(FLAG_NHML1_BOULDER)
	}
	if (var(VAR_FINAL_BATTLE) == 0) {
		setflag(FLAG_NHML1_FINAL_BATTLE)
	}
	else {
		clearflag(FLAG_NHML1_FINAL_BATTLE)
	}
	end
}

script NHML1_EveryFrame {
	if (flag(FLAG_P01_HONOR_MODE) && flag(FLAG_P01_LOAD_SAVEGAME)) {
		lock
		clearflag(FLAG_P01_LOAD_SAVEGAME)
		buffernumberstring(STR_VAR_1, VAR_P01_CALC)
		call(Common_EventScript_SaveGame)
		closemessage
		release
	}
	setvar(VAR_TEMP_2, 1)
}


// ############### Regular scripts ###############

script NHML1_Trigger_WarpL2 {
	lockall
    randomnr(3)
    setorcopyvar(VAR_NHM_L2_RIDDLE, VAR_RESULT) //set var for fossil riddle
	setflag(FLAG_NHML2_RIDDLE_START)
	clearflag(FLAG_NHML2_RIDDLE_STEP1_CHECK)
	clearflag(FLAG_NHML2_RIDDLE_STEP2_CHECK)
	warp(MAP_NATIONAL_HISTORY_MUSEUM_L2, 27, 3)
	releaseall
	end
}

script NHML1_Judge {
	lock
	faceplayer
	msgbox(format("What an exquisite place! \lAll these artifacts are just mezmerizing. \pAnd have you noticed these special wallpapers they use? \lWonderful!"), MSGBOX_AUTOCLOSE)
	waitmessage
	setflag(FLAG_NHML1_WALLPAPER_GUY)
	release
	end
}

script NHML1_Artist {
	call(FortreeCity_House1_EventScript_Trader3)
	setflag(FLAG_NHML2_TEST_OBTAIN_CLEFAIRY)
	end
}

script NHML1_ItemBall {
	lock
	msgbox(format("You found an abandoned PokeBall with a SKITTY inside."), MSGBOX_AUTOCLOSE)
	waitmessage
	fadescreen(FADE_TO_BLACK)
	removeobject(3, MAP_NATIONAL_HISTORY_MUSEUM_L1)
	setflag(FLAG_NHML1_ITEMBALL)
	fadescreen(FADE_FROM_BLACK)
    givemon(SPECIES_SKITTY, 20)
    setflag(FLAG_NHML2_TEST_OBTAIN_SKITTY)
	release
	end
}

script NHML1_Boulder {
	//do nothing
}

script NHML1_Cooltrainer {
	trainerbattle_single(TRAINER_NHM_HAIL, format("Oh hey, where are you trying to go?"), "Bye, bye!")
	end
}

script NHML1_Juan {
	lock
	faceplayer
	msgbox(format("I am sorry for any inconvenience!"), MSGBOX_AUTOCLOSE)
	release
	end
}

script NHML1_Trigger_Juan {
	lock
	if (!flag(FLAG_NHML1_YAMASK_TAKEN)) {
		applymovement(5, Move_exclmark)
        waitmovement(0)
		applymovement(OBJ_EVENT_ID_PLAYER, Move_lookup)
        waitmovement(0)
        msgbox(format("Where did you get this Yamask from?\lIt's part of our Egyptian exhibition!"), MSGBOX_AUTOCLOSE)
	    waitmessage
        msgbox(format("Someone traded it to you for MUNCHLAX?\pPlease let me clarify this."), MSGBOX_AUTOCLOSE)
	    waitmessage
		fadescreen(FADE_TO_BLACK)
		msgbox("...", MSGBOX_AUTOCLOSE)
	    waitmessage
		//waitbuttonpress
		fadescreen(FADE_FROM_BLACK)
		msgbox(format("I am sorry, you will have to return it to me.\pHowever, I managed to get the kid you traded with and will give you MUNCHLAX back in return!"), MSGBOX_AUTOCLOSE)
	    waitmessage
		
		getpartymonslot(SPECIES_YAMASK)
		special(DeletePartyMon) //using VAR_RESULT from above macro
		setflag(FLAG_GIVE_BACK_MUNCHLAX)
		givemon(SPECIES_MUNCHLAX, 20, ITEM_LAGGING_TAIL)
		msgbox(format("{PLAYER} handed over YAMASK and received MUNCHLAX back."))
		waitmessage
		setflag(FLAG_NHML1_YAMASK_TAKEN)
		setflag(FLAG_NHML3_Trader) //hide trader on floor 3
	}
	release
	end
}

script NHML1_Trigger_Final_Battle_Up {
	lock
	clearflag(FLAG_NHML1_FINAL_BATTLE)
	addobject(6, MAP_NATIONAL_HISTORY_MUSEUM_L1)
	setobjectxy(6, 21, 17)
	applymovement(6, Move_to_uppos)
	waitmovement(0)
	setorcopyvar(VAR_FINAL_BATTLE, 1)
	release
	call(NHML1_Cooltrainer)
	end
}

script NHML1_Trigger_Final_Battle_Dn {
	lock
	clearflag(FLAG_NHML1_FINAL_BATTLE)
	addobject(6, MAP_NATIONAL_HISTORY_MUSEUM_L1)
	setobjectxy(6, 21, 17)
	applymovement(6, Move_to_dnpos)
	waitmovement(0)
	setorcopyvar(VAR_FINAL_BATTLE, 1)
	release
	call(NHML1_Cooltrainer)
	end
}

script NHML1_Counter {
	lock
	setrespawn(HEAL_LOCATION_NATIONAL_HISTORY_MUSEUM_F1)
	msgbox(format("Good bye!\pThank you for visiting us!"), MSGBOX_AUTOCLOSE)
	waitmessage
	setorcopyvar(VAR_NHML1_COUNTER_TRIGGER, 1)
	release
	end
}

script NHML1_FinishGame {
	lock
	fadescreen(FADE_TO_BLACK)
	special(StopPlayTimer)
	specialvar(VAR_RESULT, GetPlayTimeHours)
	buffernumberstring(STR_VAR_1, VAR_RESULT)
	specialvar(VAR_RESULT, GetPlayTimeMinutes)
	buffertimestring(STR_VAR_2, VAR_RESULT)
	specialvar(VAR_RESULT, GetPlayTimeSeconds)
	buffertimestring(STR_VAR_3, VAR_RESULT)
	msgbox(Text_Finale)
	waitmessage
	end
}

script NHML1_Sign_Brochures {
	lock
    msgbox(format("Just some brochures about upcoming exhibitions."), MSGBOX_AUTOCLOSE)
    waitmessage
    release
    end
}

script NHML1_Sign_Flame_Orb {
	lock
	showmonpic(SPECIES_MUNCHLAX, 10, 3)
	//showitempic(ITEM_FLAME_ORB, 1, 1)
	msgbox(format("Hint about flame orb TBD"))
	waitmessage
	hidemonpic
	release
	end
}

script NHML1_Sign_Replica {
	lock
	msgbox(format("A very realistic replica of a Mamoswine."))
	waitmessage
	release
	end
}

script NHML1_Sign_Desk {
	lock
	msgbox(format("Thank you for your visit!"))
	waitmessage
	release
	end
}


// ############### movements ###############

movement Move_lookup {
	face_up
}

movement Move_to_uppos {
	face_up
	walk_up
	face_left
	walk_left *3
}

movement Move_to_dnpos {
	face_up
	face_left
	walk_left *3
}


// ############### texts ###############

text Text_Finale {
	format("Congratulations {PLAYER}! \pYou have completed the challenge. Well done! \pYour final time is {STR_VAR_1}:{STR_VAR_2}:{STR_VAR_3}.")
}
